buildscript {
    repositories {
        maven {
            name 'JFrog OSS snapshot repo'
            url  'https://oss.jfrog.org/oss-snapshot-local/'
        }
        jcenter()
    }

    dependencies {
        classpath 'me.champeau.gradle:jmh-gradle-plugin:0.2.0'
        classpath 'me.champeau.gradle:antlr4-gradle-plugin:0.1'
    }
}

apply plugin: 'me.champeau.gradle.jmh'
apply plugin: 'me.champeau.gradle.antlr4'
apply plugin: 'java'
apply plugin: 'nebula.source-jar'

dependencies {
    compile project(':java-core')

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.slf4j:slf4j-simple:1.7.13'
}

task copyAntlr(type: Copy) {
    from "${antlr4.output}"
    into "${sourceSets.main.java.srcDirs[0]}/laser/raptor/antlr/generated"
    include "*.java"
    include "*.tokens"
}

// make the Java compile task depend on the antlr4 task
copyAntlr.dependsOn antlr4
compileJava.dependsOn copyAntlr


// add antlr4 to classpath
configurations {
    compile.extendsFrom antlr4
}